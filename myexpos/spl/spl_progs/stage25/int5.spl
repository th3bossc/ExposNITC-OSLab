[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 13] = SP;
alias userSP R0;
userSP = SP;

SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 - 1;

alias callNumber R1;
callNumber = [[PTBR + 2*(userSP-5)/512]*512 + (userSP-5)%512];

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = callNumber;

alias returnAddress R2;
returnAddress = [PTBR + 2*(userSP-1)/512]*512 + (userSP-1)%512;

//open system call
if (callNumber == 2) then
    breakpoint;
    alias counter R3;
    counter = 0;
    alias resourceTable R4;
    resourceTable = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 + RESOURCE_TABLE_OFFSET;
    while (counter < 8) do
        if ([resourceTable + counter*2 + 0] == -1) then 
            break;
        endif;
        counter = counter+1;
    endwhile;
    if (counter == 8) then
        [returnAddress] = -3;
    else
        alias fileName R5;
        fileName = [[PTBR + 2*(userSP-4)/512]*512 + (userSP-4)%512];
        alias fileTableEntry R6;
        multipush(R0, R1, R2, R3, R4, R5); //open function
        R1 = 3;
        R2 = fileName;
        call FILE_MANAGER;
        fileTableEntry = R0;
        multipop(R0, R1, R2, R3, R4, R5);

        if (fileTableEntry < 0) then 
            [returnAddress] = fileTableEntry;
        else
            [resourceTable + counter*2 + 0] = FILE;
            [resourceTable + counter*2 + 1] = fileTableEntry;
            [returnAddress] = counter;
        endif;
    endif;
    breakpoint;
endif;

//close system call
if (callNumber == 3) then 
    breakpoint;
    alias fileDescriptor R3;
    fileDescriptor = [[PTBR + 2*(userSP-4)/512]*512 + (userSP-4)%512];
    if (fileDescriptor < 0 || fileDescriptor > 7) then 
        [returnAddress] = -1;
    else
        alias resourceTableEntry R4;
        resourceTableEntry = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 + RESOURCE_TABLE_OFFSET + fileDescriptor*2;

        if ([resourceTableEntry + 0] != FILE) then
            [returnAddress] = -1;
        else
            alias openFileTableEntry R5;
            openFileTableEntry = [resourceTableEntry + 1];

            multipush(R0, R1, R2, R3, R4, R5); //close function
            R1 = 4;
            R2 = openFileTableEntry;
            call FILE_MANAGER;
            multipop(R0, R1, R2, R3, R4, R5);

            [resourceTableEntry + 0] = -1;
            [returnAddress] = 0;
        endif;
    endif;
    breakpoint;
endif;

if (callNumber == 6) then 
    alias fileDescriptor R3;
    alias offset R4;
    alias returnAddress R5;
    fileDescriptor = [[PTBR + 2*(userSP-4)/512]*512 + (userSP-4)%512];
    offset = [[PTBR + 2*(userSP-3)/512]*512 + (userSP-3)%512];
    returnAddress = [PTBR + 2*(userSP-1)/512]*512 + (userSP-1)%512;

    if (fileDescriptor < 0 || fileDescriptor > 7) then 
        [returnAddress] = -1;
    else
        alias resourceTableEntry R6;
        resourceTableEntry = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 + RESOURCE_TABLE_OFFSET + fileDescriptor*2;
    
        if ([resourceTableEntry + 0] != FILE) then
            [returnAddress] = -1;
        else
            alias openFileTableEntry R7;
            alias inodeTableEntry R8;
            openFileTableEntry = [resourceTableEntry + 1];
            inodeTableEntry = [OPEN_FILE_TABLE + openFileTableEntry*4 + 0];

            multipush(R0, R1, R2, R3, R4, R5, R6, R7, R8); 
            R1 = 4;
            R2 = inodeTableEntry;
            R3 = [SYSTEM_STATUS_TABLE+1];
            call RESOURCE_MANAGER;
            R9 = R0;
            multipop(R0, R1, R2, R3, R4, R5, R6, R7, R8);

            if (R9 == -1) then
                [returnAddress] = -1;
            else
                alias lseek R9;
                lseek = OPEN_FILE_TABLE + openFileTableEntry*4 + 2;

                alias fileSize R10;
                if (inodeTableEntry == INODE_ROOT) then
                    fileSize = 480;
                else
                    fileSize = [INODE_TABLE + inodeTableEntry*16 + 2];
                endif;
                
                if ([lseek] + offset < 0) then 
                    multipush(R0, R1, R2, R3, R4, R5, R6, R7, R8, R9, R10);
                    R1 = 5;
                    R2 = inodeTableEntry;
                    R3 = [SYSTEM_STATUS_TABLE+1];
                    call RESOURCE_MANAGER;
                    multipop(R0, R1, R2, R3, R4, R5, R6, R7, R8, R9, R10);

                    [returnAddress] = -2;
                else
                    if (offset == 0) then 
                        [lseek] = 0;
                    else
                        if ([lseek] + offset > fileSize) then 
                            [lseek] = fileSize;
                        else
                            [lseek] = [lseek] + offset;
                        endif;
                    endif;


                    multipush(R0, R1, R2, R3, R4, R5, R6, R7, R8, R9, R10);
                    R1 = 5;
                    R2 = inodeTableEntry;
                    R3 = [SYSTEM_STATUS_TABLE+1];
                    call RESOURCE_MANAGER;
                    multipop(R0, R1, R2, R3, R4, R5, R6, R7, R8, R9, R10);

                    [returnAddress] = 0;
                endif;
            endif;
        endif;
    endif;
endif;

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = 0;
SP = userSP;
ireturn;