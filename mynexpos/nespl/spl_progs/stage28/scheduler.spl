multipush(BP);

alias processTableEntry R1;
processTableEntry = PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 5*CORE + 1]*16;

[processTableEntry + 12] = SP % 512;
[processTableEntry + 14] = PTBR;
[processTableEntry + 15] = PTLR;

multipush(R1);
R1 = ACQUIRE_SCHED_LOCK;
call ACCESS_CONTROL;
multipop(R1);

alias newPid R2;

alias newProcessTableEntry R3;
alias otherCorePid R4;

newPid = -1;

if(CORE == PRIMARY_CORE) then
    if ([SYSTEM_STATUS_TABLE + 5] != 0) then
        if ([PROCESS_TABLE + 16*SWAPPER_DAEMON + 4] != WAIT_DISK) then 
            newPid = SWAPPER_DAEMON;
        else
            newPid = IDLE_PROCESS;
        endif;
    endif;

    if ([SYSTEM_STATUS_TABLE + 7] == 1 && [SYSTEM_STATUS_TABLE + 6] != IDLE2_PROCESS) then
        newPid = IDLE_PROCESS;
    endif;
    otherCorePid = [SYSTEM_STATUS_TABLE + 6];
else
    if ([SYSTEM_STATUS_TABLE + 5] != 0 || [SYSTEM_STATUS_TABLE + 7] == 1) then
        newPid = IDLE2_PROCESS;
    endif;
    otherCorePid = [SYSTEM_STATUS_TABLE + 1];
endif;

if (newPid == -1) then 
    alias counter R5;
    alias currPid R6;
    currPid = [SYSTEM_STATUS_TABLE + 5*CORE + 1];
    counter = currPid + 1;

    while (counter < MAX_PROC_NUM) do
        processTableEntry = PROCESS_TABLE + counter*16;
        if (counter != otherCorePid && [processTableEntry + 6] == 0 && ([processTableEntry + 4] == READY || [processTableEntry + 4] == CREATED)) then
            if (CORE == PRIMARY_CORE && counter != IDLE2_PROCESS && counter != IDLE_PROCESS && counter != SWAPPER_DAEMON) then
                newPid = counter;
                break;
            else
                if (CORE == SECONDARY_CORE && counter != IDLE2_PROCESS && counter != IDLE_PROCESS && counter != LOGIN_PROCESS && counter != SHELL_PROCESS && counter != SWAPPER_DAEMON) then
                    newPid = counter;
                    break;
                endif;
            endif;
        endif;
        counter = counter + 1;
    endwhile;

    if (newPid == -1) then 
        counter = 0;
        while (counter < currPid) do
            processTableEntry = PROCESS_TABLE + counter*16;
            if (counter != otherCorePid && [processTableEntry + 6] == 0 && ([processTableEntry + 4] == READY || [processTableEntry + 4] == CREATED)) then
                if (CORE == PRIMARY_CORE && counter != IDLE2_PROCESS && counter != IDLE_PROCESS && counter != SWAPPER_DAEMON) then
                    newPid = counter;
                    break;
                else
                    if (CORE == SECONDARY_CORE && counter != IDLE2_PROCESS && counter != IDLE_PROCESS && counter != LOGIN_PROCESS && counter != SHELL_PROCESS && counter != SWAPPER_DAEMON) then
                        newPid = counter;
                        break;
                    endif;
                endif;
            endif;
            counter = counter + 1;
        endwhile;
    endif;

    if (newPid == -1) then 
        if (CORE == PRIMARY_CORE) then
            newPid = IDLE_PROCESS;
        else
            newPid = IDLE2_PROCESS;
        endif;
    endif;
endif;

newProcessTableEntry = PROCESS_TABLE + newPid*16;

[SYSTEM_STATUS_TABLE + 5*CORE + 1] = newPid;

PTBR = [newProcessTableEntry + 14];
PTLR = [newProcessTableEntry + 15];

if ([newProcessTableEntry + 4] == CREATED) then
    [newProcessTableEntry + 4] = RUNNING;
    BP = [[newProcessTableEntry + 11]*512];

    multipush(R1, R2, R3);
    R1 = RELEASE_LOCK;
    R2 = SCHED_LOCK;
    call ACCESS_CONTROL;
    multipop(R1, R2, R3);

    SP = [newProcessTableEntry + 13];
    ireturn;
endif;

[newProcessTableEntry + 4] = RUNNING;

multipush(R1, R2, R3);
R1 = RELEASE_LOCK;
R2 = SCHED_LOCK;
call ACCESS_CONTROL;
multipop(R1, R2, R3);

SP = [newProcessTableEntry + 11]*512 + [newProcessTableEntry + 12];

multipop(BP);
return;